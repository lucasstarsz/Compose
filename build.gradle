plugins {
    // Just a normal application id, nothing to see here
    id 'application'

    // jlink/jpackage plugins are always nice
    id 'org.beryx.jlink' version '2.24.1'
}

group('org.lucasstarsz')
version('0.2.1')

// Build
def currentOs = org.gradle.internal.os.OperatingSystem.current()

java {
    modularity.inferModulePath.set(true)
}

application {
    mainModule.set('Compose.main')
    mainClass.set('org.lucasstarsz.composeapp.core.ComposeApp')
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.fxmisc.richtext:richtextfx:0.10.9'
    implementation 'org.fxmisc.undo:undofx:2.1.1'
    implementation 'org.fxmisc.flowless:flowless:0.6.9'

    // since gradle 6.6, we grab dependencies straight from the source
    def platform = (currentOs.windows ? 'win' : currentOs.linux ? 'linux' : currentOs.macOsX ? 'mac' : '')
    implementation "org.openjfx:javafx-base:17:${platform}"
    implementation "org.openjfx:javafx-controls:17:${platform}"
    implementation "org.openjfx:javafx-graphics:17:${platform}"
    implementation "org.openjfx:javafx-fxml:17:${platform}"
}

// Testing
// TODO: add tests for utility methods

test {
    useJUnitPlatform()
}

// Packaging & Distribution

boolean usingSnapshot = false
def snapshotVersion = "SNAPSHOT"
def distributionName = "Compose Text Editor${usingSnapshot ? "-$snapshotVersion" : ""}"

jlink {

    addExtraDependencies("javafx")
    options.addAll('--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages')

    launcher {
        name = distributionName
        noConsole = true
    }

    jpackage {
        // cross-platform options
        jvmArgs += ['-XX:+CreateCoredumpOnCrash', '-XX:+UseZGC']
        installerOptions += [
                '--description', 'A clean, simple text editor.',
                '--vendor', 'Andrew Dey',
                '--copyright', 'CopyrightÂ© 2020-2021 Andrew Dey',
                '--license-file', 'LICENSE.txt',
                '--file-associations', 'src/main/resources/extensions/ext_txt.properties',
                '--file-associations', 'src/main/resources/extensions/ext_text.properties',
                '--app-version', project.version
        ]

        // platform-specific options
        def iconPath = 'src/main/resources/icons/compose'

        if (currentOs.windows) {
            installerType = "msi"
            icon = "${iconPath}_ico.ico"

            installerOptions += [
                    '--win-per-user-install',
                    '--install-dir', distributionName,
                    '--win-dir-chooser',
                    '--win-shortcut',
                    '--win-menu', '--win-menu-group', 'Compose Text Editor'
            ]
        } else if (currentOs.linux) {
            installerType = "deb"
            icon = "${iconPath}_png.png"

            installerOptions += [
                    '--linux-menu-group', 'Office',
                    '--linux-shortcut',
                    '--linux-deb-maintainer', 'andrewrcdey@gmail.com'
            ]
        } else if (currentOs.macOsX) {
            installerType = "pkg"
            icon = "${iconPath}_icns.icns"

            installerOptions += [
                    '--mac-package-name', 'Compose'
            ]
        }
    }
}

// Java 17 is a must!
compileJava {
    options.release.set(17)
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}
